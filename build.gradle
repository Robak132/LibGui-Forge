plugins {
	id 'idea'
	id 'java'
	id 'java-library'
	id 'maven-publish'
	id 'net.minecraftforge.gradle' version '6.0.+'
	id 'org.parchmentmc.librarian.forgegradle' version '1.+'
	id 'org.spongepowered.mixin' version '0.7.+'
}

repositories {
	maven { url "https://maven.juuxel.me/" }
	maven { url "https://maven.shedaniel.me/" }
	maven { url "https://maven.minecraftforge.net" }
	mavenCentral()
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"

minecraft {
	// Use Parchment mappings that match the Forge/Minecraft version (1.20.1)
	mappings channel: 'parchment', version: '2023.09.03-1.20.1'
	copyIdeResources = true
	generateRunFolders = true
	accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

	runs {
		configureEach {
			workingDirectory project.file('run')
			property 'forge.logging.markers', 'REGISTRIES'
			property 'forge.logging.console.level', 'debug'

			mods {
				"${mod_id}" {
					source sourceSets.main
				}
			}
		}

		client {
			property 'mixin.env.remapRefMap', 'true'
			property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
			property 'forge.enabledGameTestNamespaces', mod_id
		}

		server {
			property 'mixin.env.remapRefMap', 'true'
			property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
			property 'forge.enabledGameTestNamespaces', mod_id
		}

		gameTestServer {
			property 'mixin.env.remapRefMap', 'true'
			property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
			property 'forge.enabledGameTestNamespaces', mod_id
		}

		data {
			workingDirectory project.file('run-data')

			args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
		}
	}
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    minecraft "net.minecraftforge:forge:1.20.1-47.4.13"
    implementation 'org.projectlombok:lombok:1.18.38'
	implementation "me.shedaniel.cloth:cloth-config-forge:11.1.136"
    implementation 'com.google.code.gson:gson:2.13.2'
    api "io.github.juuxel:libninepatch:1.2.0"

    compileOnly("org.projectlombok:lombok:1.18.42")
    annotationProcessor("org.projectlombok:lombok:1.18.42")

    testCompileOnly("org.projectlombok:lombok:1.18.42")
    testAnnotationProcessor("org.projectlombok:lombok:1.18.42")

	annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

java {
	// Ensure source uses Java 21 features and bytecode targets Java 17
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
	toolchain { languageVersion = JavaLanguageVersion.of(17) }
}

mixin {
	// MixinGradle Settings
	add sourceSets.main, 'mixins.libgui_forge.refmap.json'
	config 'mixins.libgui_forge.json'

	debug.verbose = true
	debug.export = true
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8'
	options.release = 17
}

tasks.named('jar', Jar).configure {
	manifest {
		attributes([
			'Specification-Title'     : mod_id,
			'Specification-Vendor'    : mod_authors,
			'Specification-Version'   : '1',
			'Implementation-Title'    : project.name,
			'Implementation-Version'  : project.jar.archiveVersion,
			'Implementation-Vendor'   : mod_authors,
			'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}
	finalizedBy 'reobfJar'
}

tasks.register('cleanJar') {
	delete 'build/libs'
}

tasks.named('jarJar') {
    // ...
}

project.publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId base.archivesName.get()
            from components.java

            artifact(tasks.reobfJar) {
                classifier = "all"
            }
        }
    }

    repositories {
        if (project.hasProperty("mavenUsername") && project.hasProperty("mavenPassword") && project.hasProperty("mavenURL")) {
            project.logger.lifecycle("Adding maven from secrets")

            maven {
                credentials {
                    username = project.property("mavenUsername") as String
                    password = project.property("mavenPassword") as String
                }
                url = URI.create(project.property("mavenURL") as String)
            }
        }

        if (project.hasProperty("mavendir")) {
            maven { url mavendir }
        }
    }
}

publishMods {
    file = reobfJar.archiveFile
    changelog = ChangelogText.getChangelogText(project).toString()
    type = STABLE
    version = "mc$minecraft_version-$mod_version"
    displayName = "$mod_name $mod_version for mc$minecraft_version"
    modLoaders.add("forge")
    modLoaders.add("neoforge")

    github {
        accessToken = providers.environmentVariable("GITHUB_TOKEN")
        repository = "Robak132/LibGui-Forgified"
        commitish = gitHash
        tagName = "mc$minecraft_version-$mod_version"
        file.unset()
        file.unsetConvention()

        allowEmptyFiles = true
    }
}

String calculateGitHash() {
    try {
        ExecOutput output = providers.exec {
            commandLine("git", "rev-parse", "HEAD")
        }
        return output.standardOutput.asText.get().trim()
    } catch (Throwable ignored) {
        return "unknown"
    }
}

boolean hasUnstaged() {
    try {
        ExecOutput output = providers.exec {
            commandLine("git", "status", "--porcelain")
        }
        String result = output.standardOutput.asText.get().replace("/M gradlew(\\.bat)?/", "").trim()
        if (!result.isEmpty())
            println("Found stageable results:\n ${result}\n")
        return !result.isEmpty()
    } catch (Throwable ignored) {
        return false
    }
}